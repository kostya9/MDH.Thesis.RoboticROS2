env int msgDelay = 20;
env int pathfinderDelay = 50;
env int moveDelay = 200;

env int MAPSIZE = 20;

env double laserDist = 3;
env double INFINITY = 9999;
env int lasers = 10;
env double pi = 3.14;
env double minAngle = pi / 4;
env double maxAngle = 3 * pi / 4;
env double angleIncrement = (maxAngle - minAngle) / lasers;
env double laserLength = 10;
env double steps = 15;
env double stepLength = laserLength / steps;

env int COLLISION = -1;


// ROTATIONS
env int NORTH = 0;
env int EAST = 1;
env int SOUTH = 2;
env int WEST = 3;

reactiveclass RobotHardware(10) {
	knownrebecs {
		Robot robot;
	}
	
	statevars {
		int[20][20] map;
		int posX;
		int posY;
		int rot;
		boolean moving;
		
		int nextRot;
		
		// variables for assertion
		int prevPosX;
		int prevPosY;
		int prevRot;
		boolean isFirstState;
	}
	
	RobotHardware()
	{
		moving = true;
		isFirstState = true;
		
		nextRot = rot;
		
		setCollisions();
		laserScanSend();
		moveActuator() after(moveDelay);
	}
	
	// USE THIS METHOD TO SET COLLISIONS ON THE MAP
	void setCollisions() { 
		map[1][0] = COLLISION;
	}
	
	double tan(double angle) {
		if(angle > (pi / 2)) {
			angle = angle - pi;
		}
		
		double x = angle;
		// taylor series expansion
		return x + x*x*x / 3 + 2*x*x*x*x*x/15; 
	}
	
	double abs(double x) {
		if(x < 0) {
			return -x;
		}
		
		return x;
	}
	
	double sqrt(double x) {
		double prev = 4;
		double next = 5;
		while(abs(prev - next) > 0.1) {
			prev = next;
			next = 0.5 * ((x/prev) + prev);
		}
		
		return next;
	}
	
	double getDistanceToCollision(double dy, double curAngle) {
		// f(x) = kx
		// x = f(x) / k
		// where k - the tangent of tilt angle
		double dx = dy / tan(curAngle);
		int turnedX, turnedY;
				
		if(rot == NORTH)
		{
			turnedX = (int)dx;
			turnedY = (int)dy;
		}
		else if(rot == EAST)
		{
			turnedX = (int)dy;
			turnedY = (int)-dx;
		}
		else if(rot == SOUTH)
		{
			turnedX = (int)-dx;
			turnedY = (int)dy;
		}
		else if (rot == WEST)
		{
			turnedX = (int)-dy;
			turnedY = (int)dx;
		}
		
		int yIndx = posY + turnedY;
		int xIndx = posX + turnedX;
		
		// check collision with end of map
		if ((yIndx >= 20) || (yIndx < 0)) {
			return 0;
		}
		
		// check collision with end of map
		if((xIndx >= 20) || (xIndx < 0)) {
			return 0;
		}
		
		if(map[xIndx][yIndx] == COLLISION) {
			return sqrt((turnedY + 0.5) * (turnedY + 0.5) + (turnedX + 0.5) * (turnedX + 0.5));
		}
		
		return INFINITY;
	}
	
	// Send laser data to robot software
	msgsrv laserScanSend() {
		double[10] dists;
		// init array
		for(int i = 0; i < lasers; i++) {
			dists[i] = INFINITY;
		}

		// for each laser 
		// go through distances from 0 to laserLength
		// and if there is collision (and it is the first collision) 
		//     write the distance to it
		// else
		//     do nothing
		double curAngle = minAngle;
		for(int i = lasers - 1; i >= 0; i--) {
		
			// test the cells for collision where f(x) = dy
			// f(x) - laser ray function
			for (double dy = 0; dy <= laserLength; dy += stepLength) {
				if(dists[i] == INFINITY) {
					dists[i] = getDistanceToCollision(dy, curAngle);
				}
			}
			curAngle = curAngle + angleIncrement;
		}
		
		robot.onLaserScan(posX, posY, rot, moving, dists);
		
		self.laserScanSend() after(msgDelay);
	}
	
	// Move the actual robot (change pos)
	msgsrv moveActuator() {
		if(nextRot != rot) {
			// rotating
			prevRot = rot;
			rot = nextRot;
		}
		// no movement when finished
		else if(moving)
		{
			int dx, dy;
			if(rot == NORTH)
			{
				dx = 0;
				dy = 1;
			}
			else if(rot == EAST)
			{
				dx = 1;
				dy = 0;
			}
			else if(rot == SOUTH)
			{
				dx = 0;
				dy = -1;
			}
			else if (rot == WEST)
			{
				dx = -1;
				dy = 0;
			}
			
			self.prevPosX = posX;
			self.posX += dx;
			self.prevPosY = posY;
			self.posY += dy;
		}
		
		self.isFirstState = false;
		moveActuator() after(moveDelay);
	}
	
	msgsrv move() {
		moving = true;
	}
	
	msgsrv stop() {
		moving = false;
	}
	
	msgsrv turnLeft() {
		nextRot = (rot - 1) % 4;
	}
	
	msgsrv turnRight() {
		nextRot = (rot + 1) % 4;
	}
}

reactiveclass Pathfinder(11)
{
	statevars {
		int xPos;
	    int yPos;
	    
	    int targetX;
	    int targetY;
	    
	    boolean[20][20] map;
	    int[20][20] parents;
	    
	    int[30] openList;
	    int[30] closedList;
	    double[20][20] hs;
	    double[20][20] gs;
	    double[20][20] fs;
	    
	    boolean changed;
	    
	    int changeTimes;
	}
	
	msgsrv setTargetPosition(int x, int y) {
		self.targetX = x;
		self.targetY = y;
		self.changed = true;
	}
	
	double getYawFromRot(int rot) {
		if(rot == NORTH)
		{
			return pi/2.0;
		}
		else if(rot == EAST)
		{
			return 0;
		}
		else if(rot == SOUTH)
		{
			return -pi/2.0;
		}
		else if (rot == WEST)
		{
			return pi;
		}
	}
	
	msgsrv setPosition(int nextxPos, int nextyPos, int rot) {
		self.xPos = nextxPos;
		self.yPos = nextyPos;
	}
	
	double distTo(double x, double y, double xTarget, double yTarget) {
		double dy = y - yTarget;
		double dx = x - xTarget;
		return dy * dy + dx * dx;
	}
	
	int[30] clearList(int[30] list) {
		for(int i = 0; i < 30; i++) {
			list[i] = -1;
		}
		
		return list;
	}
	
	int[30] addToList(int[30] list, int value) {
		boolean added = false;
		for(int i = 0; i < 30; i++) {
			if(list[i] == -1) {
				added = true;
				list[i] = value;
				break;
			}
		}
		
		assertion(added, "buffer overflow");
		return list;
	}
	
	int[30] removeFromList(int[30] list, int idx) {
		int lastIdx = 29; 
		for(int i = 0; i < 30; i++) {
			if(list[i] == -1) {
				lastIdx = i - 1;
				break;
			}
		}
		
		// Swap with the last element
		// and erase new last element
		int toDelete = list[idx];
		list[idx] = list[lastIdx];
		list[lastIdx] = -1;
		
		return list;
	}
	
	int findIdxInList(int[30] list, int value) {
		for(int i = 0; i < 30; i++) {
			if(list[i] == value) {
				return i;
			}
			
			if(list[i] == -1) {
				return -1;
			}
		}
		
		return -1;
	}
	
	int size(int[30] list) {
		for(int i = 0; i < 30; i++) {
			if(list[i] == -1) {
				return i;
			}
		}
		
		return 30;
	}
	
	double cos(double x) {
		return 1 - x*x/2 + x*x*x*x/24- x*x*x*x*x*x/720;
	}
	
	double sin(double x) {
		return x - x*x*x/6 + x*x*x*x*x/120- x*x*x*x*x*x*x/5040;
	}
	
	int posToIdx(int x, int y) {
		return x + y * 20;
	}
	
	int idxToY(int idx) {
		return idx / 20;
	}
	
	int idxToX(int idx) {
		return idx % 20;
	}
	
	int idxOfSmallestF() {
		int smallestIdx = 0;
		double smallestF = openList[0];
		for(int i = 0; i < 127; i++) {
			int cur = openList[i];
			if(cur == -1) {
				break;
			}
			
			int x = idxToX(cur);
			int y = idxToY(cur);
			double curF = fs[x][y];
			if(curF < smallestF) {
				smallestF = curF;
				smallestIdx = i;
			}
		}
		
		return smallestIdx;
	}
	
	int[30] generatePathFromTarget() {
		int[30] path;
		path = clearList(path);
		
		int els = 0;
		int parent = posToIdx(targetX, targetY);
		while(true) {
			int x = idxToX(parent);
			int y = idxToY(parent);
			if(x == xPos && y == yPos) {
				break;
			}
			
			path = addToList(path, parent);
			els++;
			parent = parents[x][y];
		}
		
		
		// Reverse the path;
		for(int idx = 0; idx < els / 2; idx++) {
			int endElIdx = els - idx - 1;
			int endEl = path[endElIdx];
			path[endElIdx] = path[idx];
			path[idx] = endEl;
		}
		
		return path;
	}
	
	msgsrv calcPath() {
		if(changed) {
			changed = false;
			
			openList = clearList(openList);
			closedList = clearList(closedList);
			
			int curPos = posToIdx(xPos, yPos);
			openList = addToList(openList, curPos);
			
			while(size(openList) != 0) {
				int curOpenListIdx = idxOfSmallestF();
				int curNodeIdx = openList[curOpenListIdx];
				openList = removeFromList(openList, curOpenListIdx);
				closedList = addToList(closedList, curNodeIdx);
				
				int x = idxToX(curNodeIdx);
				int y = idxToY(curNodeIdx);
				if(x == targetX && y == targetY) {
					int[30] path = generatePathFromTarget();
					
					((Robot)sender).onNewPath(path);
					break;
				}
				
				// for each neighbor
				for(int dx = -1; dx <= 1; dx++) {
					for(int dy = -1; dy <= 1; dy++) {
						
						// IGNORE DIAGONALS
						// TODO: HANDLE DIAGONALS
						if(dx + dy != 1 && dx + dy != -1) {
							continue;
						}
						
						int neighborX = x + dx;
						int neighborY = y + dy;
						int neighborIdx = posToIdx(neighborX, neighborY);
						
						if(neighborX < 0 || neighborX >= 20 ||
							neighborY < 0 || neighborY >= 20) {
							continue;
						}
						
						boolean collision = map[neighborX][neighborY];
						if(collision) {
							continue;
						}
						
						int alreadyInClosedList = findIdxInList(closedList, neighborIdx);
						if(alreadyInClosedList != -1) {
							continue;
						}
						
						double g = gs[x][y] + 1; // TODO: handle diagonals
						double h = distTo(neighborX, neighborY, targetX, targetY);
						double f = g + h;
						
						int alreadyInOpenList = findIdxInList(openList, neighborIdx);
						if(alreadyInOpenList != -1) {
							if(g > gs[neighborX][neighborY]) {
								continue;
							}
						}
						
						parents[neighborX][neighborY] = curNodeIdx;
						gs[neighborX][neighborY] = g;
						hs[neighborX][neighborY] = h;
						fs[neighborX][neighborY] = f;
						openList = addToList(openList, neighborIdx);
					}
				}
			}
		}
	}
	
	msgsrv addCollision(double distance, double dYaw, int posX, int posY, int rot) {
		double rotYaw = getYawFromRot(rot);
		double yaw = rotYaw + dYaw - pi / 2;
	    double colTargetX = posX + distance * cos(yaw);
	    double colTargetY = posY + distance * sin(yaw);
	    
	    int targetXIndex = (int)(colTargetX);
	    int targetYIndex = (int)(colTargetY);
	
	    if(targetXIndex < 0 || targetXIndex >= MAPSIZE || 
	        targetYIndex < 0 || targetYIndex >= MAPSIZE) 
	    {
	       // out of bounds
	    } else {
	    	if(!map[targetXIndex][targetYIndex]) {
	    		//assertion(false, "dist=" + distance + " yaw= " + yaw + " colTargetX=" + colTargetX + " colTargetY=" + colTargetY);
	    		self.changed = true;
	    		map[targetXIndex][targetYIndex] = true;
	    	}
	    }
	}
}

reactiveclass Robot(10) {
	knownrebecs {
		RobotHardware rh;
		Pathfinder pf;
	}
	
	statevars {
		int targetX;
		int targetY;
		boolean finish;
		int[30] path;
		int curPathIdx;
	}
	
	Robot(int x, int y) {
		self.targetX = x;
		self.targetY = y;
		self.setTargetPos(); // cannot send to knownrebec
		self.rebuildPath() after(pathfinderDelay);
	}
	
	msgsrv setTargetPos() {
		pf.setTargetPosition(self.targetX, self.targetY);
	}
	
	msgsrv rebuildPath() {
		pf.calcPath();
		
		self.rebuildPath() after(pathfinderDelay);
	}

	// Straightforward algorithm - just follow target, no collision avoidance
	msgsrv onLaserScan(int posX, int posY, int rot, boolean moving, double[10] laserDistances) {
		if(!finish) {
			pf.setPosition(posX, posY, rot);
			
			double curAngle = minAngle;
			for(int i = 0; i < 10; i++) {
				double dist = laserDistances[i];
				
				if(dist != INFINITY && dist != 0) {
					pf.addCollision(dist, curAngle, posX, posY, rot);
				}
				
				curAngle = curAngle + angleIncrement;
			}
			
			moveNaive(posX, posY, rot, moving);
		}
	}
	
	void moveNaive(int posX, int posY, int rot, boolean moving) {
		int curPathNode = path[curPathIdx];
		int nextX = curPathNode % 20;
		int nextY = curPathNode / 20;
		
		if(nextX == posX && nextY == posY && curPathNode != -1) {
			curPathIdx++;
		}
		
		if(curPathNode == -1) {
			rh.stop();
		}
		if(posX == self.targetX && posY == self.targetY) {
			if(!finish) {
				rh.stop();
				finish = true;
			}
		} else {
			boolean stop = false;
			if(nextY > posY) {
				if(rot != NORTH) {
					rh.turnRight();
					stop = true;
				}
			}
			else if(nextY < posY) {
				if(rot != SOUTH) {
					rh.turnRight();
					stop = true;
				}
			}
			else if(nextX > posX) {
				if(rot != EAST) {
					rh.turnRight();
					stop = true;
				}
			}
			else if(nextX < posX) {
				if(rot != WEST) {
					rh.turnRight();
					stop = true;
				}
			}
			
			if(stop && moving) {
				rh.stop();
			}
			else if(!moving) {
				rh.move();
			}
		}
	}
	
	msgsrv onNewPath(int[30] newPath) {
		self.path = newPath;
		self.curPathIdx = 0;
	}
}

main {
	Pathfinder pf():();
	Robot r(rh, pf):((int)5, (int)2);
	RobotHardware rh(r):();
}